apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.unquietcode.tools.flapi'
version = '2.0'
description = 'Flapi build plugin for Gradle'

sourceCompatibility = '1.8'

repositories {
	mavenLocal()
}

dependencies {
	compile gradleApi()
	compile localGroovy()

	compile 'com.unquietcode.tools.flapi:flapi-plugin:2.0'
}

task sourceJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourceJar
	archives javadocJar
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'Flapi Gradle Plugin'
				description project.description

				packaging 'jar'
				url 'https://github.com/UnquietCode/Flapi'

				scm {
					connection 'scm:git:https://github.com/UnquietCode/Flapi.git'
					developerConnection 'scm:git:https://github.com/UnquietCode/Flapi.git'
					url 'https://github.com/UnquietCode/Flapi'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'UnquietCode'
						name 'Benjamin Fagin'
						email 'blouis@unquietcode.com'
					}
				}
			}
		}
	}
}

signing {
	sign configurations.archives
}
